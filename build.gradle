import org.yaml.snakeyaml.Yaml

buildscript {
    dependencies {
        classpath 'org.yaml:snakeyaml:2.0'
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.0'

}

group = 'seu'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenCentral()
}

dependencies {
    //jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    //implementation 'org.springframework.security:spring-security-oauth2-authorization-server:1.2.3'

//    implementation 'org.keycloak:keycloak-spring-boot-starter:21.1.1'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:4.24.0'
    implementation "org.liquibase.ext:liquibase-hibernate6:4.24.0"
    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'info.picocli:picocli:4.7.6'

    //keyclock
    implementation 'org.keycloak:keycloak-admin-client:26.0.4'

    // minIO

        implementation 'io.minio:minio:8.5.7'

    // دعم JAX-RS
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'



    implementation 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}



def appProperties = new Yaml().load(file("src/main/resources/application.yml").newInputStream())
def liquibaseProperties = new Yaml().load(file("src/main/resources/liquibase.yaml").newInputStream())


liquibase {
    activities {
        main {
            changelogFile 'src/main/resources/db/changelog/changes/V' + System.currentTimeMillis() +
                    '_changelog.yaml'
            referenceUrl 'hibernate:spring:' + liquibaseProperties.getAt("entities-package-path") + '?dialect=' + liquibaseProperties.dialect
            url appProperties.spring.datasource.url
            username appProperties.spring.datasource.username
            password appProperties.spring.datasource.password
            driver liquibaseProperties.driver
        }
    }
}

